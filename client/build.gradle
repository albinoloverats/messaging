plugins {
	id "java"
	id "java-library"
	id "maven-publish"

	id "org.springframework.boot" version "4.0.2"
	id "io.spring.dependency-management"

	id "io.freefair.lombok"
	id "io.freefair.aspectj.post-compile-weaving"
	id "org.gradlex.extra-java-module-info"
}

java {
	withSourcesJar()
	withJavadocJar()
}

dependencies {
	api project(":common")

	implementation "org.springframework.boot:spring-boot-autoconfigure"
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	implementation "org.slf4j:slf4j-api"
	implementation "jakarta.annotation:jakarta.annotation-api"
	implementation "io.micrometer:micrometer-core:1.17.0-M2"
	implementation "org.aspectj:aspectjrt:1.9.25.1"
	implementation "com.jcabi:jcabi-aspects:0.26.0"
	aspect "com.jcabi:jcabi-aspects:0.26.0"


	testImplementation testFixtures(project(":common"))
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation platform("org.junit:junit-bom:6.0.3")
	testImplementation "org.junit.jupiter:junit-jupiter"
	testImplementation "org.skyscreamer:jsonassert:1.5.3"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			pom {
				artifactId = "messaging-" + project.name
				name = "net.albinoloverats.messaging:" + artifactId
				description = "Spring Boot ready messaging client. Depend on this, no need to explicitly depend on messaging-common"
				url = "https://albinoloverats.net/projects/messaging"
				licenses {
					license {
						name = "The Apache License, Version 2.0"
						url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
					}
				}
				developers {
					developer {
						name = "Ashley Anderson"
						email = "messaging@albinoloverats.net"
						organization = "albinoloverats ~ Software Development"
						organizationUrl = "https://albinoloverats.net/"
					}
				}
				scm {
					connection = "scm:git:git://github.com/albinoloverats/messaging.git"
					developerConnection = "scm:git:ssh://github.com:albinoloverats/messaging.git"
					url = "https://github.com/albinoloverats/messaging/tree/master"
				}
			}
		}
	}
	repositories {
		mavenLocal()
		maven {
			name = "SonatypeSnapshots"
			url = uri("https://central.sonatype.com/repository/maven-snapshots/")
			credentials {
				username = findProperty("sonatypeUsername") ?: System.getenv("SONATYPE_USERNAME")
				password = findProperty("sonatypePassword") ?: System.getenv("SONATYPE_PASSWORD")
			}
		}
	}
}

tasks.withType(AbstractPublishToMaven).configureEach {
	onlyIf { !version.toString().endsWith("-SNAPSHOT") || true }
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}
